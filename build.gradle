apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

/*
 * Gets the version name from the latest Git tag
 */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

version = getVersionName()
group = 'de.jodamob.android'


configurations {
    provided
}

repositories {
    mavenCentral()
}

dependencies {
	testCompile 'junit:junit:4.11'
	testCompile 'org.mockito:mockito-all:1.9.5'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

sourceSets {
    main { compileClasspath += configurations.provided }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

compileJava {
  sourceCompatibility = 1.6
  targetCompatibility = 1.6
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'SuperReflect'
                packaging 'jar'
                description 'OO wrapper for the java.lang.reflect package.'
                url 'https://github.com/dpreussler/SuperReflect'

                scm {
                    url 'scm:git@github.com:dpreussler/SuperReflect.git'
                    connection 'scm:git@github.com:dpreussler/SuperReflect.git'
                    developerConnection 'scm:git@github.com:dpreussler/SuperReflect.git'
                }

                licenses {
                    license {
                        name 'Apache 2.0'
                        url 'https://github.com/dpreussler/SuperReflect/blob/master/LICENSE.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'dpreussler'
                        name 'Danny Preussler'
                    }
                }
            }
        }
    }
}